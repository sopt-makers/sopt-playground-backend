plugins {
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id 'org.hibernate.orm' version '6.2.13.Final'
}

group = 'org.sopt.makers'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

def queryDslVersion = "5.0.0"
def mapstructVersion = "1.5.2.Final"

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.0")
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-aop"
	if (project.hasProperty('lambda')) {
		implementation("org.springframework.boot:spring-boot-starter-web") {
			exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
		}
	} else {
		// 로컬 테스트시 Tomcat 포함
		implementation "org.springframework.boot:spring-boot-starter-web"
	}
	implementation "org.springframework.boot:spring-boot-starter-mail"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
	implementation "org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j"
	implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
	implementation 'com.squareup.okhttp3:okhttp:5.3.2'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0"
	implementation 'com.bucket4j:bucket4j-core:8.4.0'
	implementation 'net.minidev:json-smart'

	// AWS Lambda Dependencies for JAR deployment
	implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.1.5'
	implementation 'com.amazonaws:aws-lambda-java-core:1.4.0'
	implementation 'com.amazonaws:aws-lambda-java-events:3.16.1'

	// log
	implementation 'net.logstash.logback:logstash-logback-encoder:7.3'

	// Postgresql Array type
	implementation('io.hypersistence:hypersistence-utils-hibernate-62:3.5.0')

	// S3
	implementation('software.amazon.awssdk:s3:2.17.285')

	implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
	runtimeOnly "io.jsonwebtoken:jjwt-impl:0.11.2"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.11.2"
	implementation("com.nimbusds:nimbus-jose-jwt:9.37.3")

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
	testCompileOnly "org.projectlombok:lombok"
	testAnnotationProcessor 'org.projectlombok:lombok'

	// Mapstruct
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	// QueryDsl
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
	annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jakarta"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Slack
	implementation 'com.slack.api:slack-api-client:1.41.0'

	// Prometheus
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// Local Cache
	implementation "com.github.ben-manes.caffeine:caffeine:3.1.6"

	// Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// Google Service
	implementation 'com.google.apis:google-api-services-sheets:v4-rev516-1.23.0'
	implementation 'com.google.auth:google-auth-library-oauth2-http:0.20.0'

	// Mac M1/M2 ARM 환경에서 Netty DNS Resolver
	implementation "io.netty:netty-resolver-dns-native-macos:4.1.82.Final:osx-aarch_64"

	// BouncyCastle for Apple JWT
	implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
	implementation 'org.bouncycastle:bcpkix-jdk18on:1.78.1'

	// ArchUnit for Native Image compatibility tests
	testImplementation 'com.tngtech.archunit:archunit-junit5:1.2.1'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// Lambda ZIP 빌드 설정 - buildZip 설정을 포함한 Lambda 최적화
task lambdaJar(type: Zip) {
	dependsOn bootJar  // bootJar 작업이 먼저 실행되도록 명시적 의존성 추가
	archiveClassifier = 'lambda'
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	zip64 = true  // 대용량 ZIP 파일 지원 (65535개 이상의 파일)

	// buildZip처럼 lib 디렉토리 구조로 패키징
	into('lib') {
		from(jar)
		from(configurations.runtimeClasspath) {
			// Tomcat 관련 제외 - Lambda에서는 불필요
			exclude "org/apache/tomcat/embed/**"
			exclude "META-INF/*.SF"
			exclude "META-INF/*.DSA"
			exclude "META-INF/*.RSA"
			exclude "META-INF/MANIFEST.MF"
			exclude "**/module-info.class"
		}
	}
}

// Lambda 빌드를 기본 빌드에 포함
build.dependsOn lambdaJar

// Spring Boot JAR 빌드 설정
jar {
	enabled = true
	archiveClassifier = ''
}

bootJar {
	enabled = true
}
